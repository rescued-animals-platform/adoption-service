import com.github.spotbugs.SpotBugsTask

buildscript {
    ext {
        springBootVersion = "2.1.6.RELEASE"
    }
}

plugins {
    id "java"
    id "idea"
    id "pmd"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "org.springframework.boot" version "2.1.6.RELEASE"
    id "info.solidsoft.pitest" version "1.4.0"
    id "com.github.spotbugs" version "2.0.0"
}

apply from: "gradle/dependencies.gradle"
apply from: "gradle/pitest.gradle"
apply from: "gradle/pmd.gradle"
apply from: "gradle/sourceset.gradle"
apply from: "gradle/idea.gradle"


group "ec.animal.adoption"

compileJava {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}

repositories {
    mavenCentral()
}

configurations {
    integrationTestCompile.extendsFrom(testCompile)
    integrationTestRuntime.extendsFrom(testRuntime)
    apiTestCompile.extendsFrom(testCompile)
    apiTestRuntime.extendsFrom(testRuntime)
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task apiTest(type: Test) {
    testClassesDirs = sourceSets.apiTest.output.classesDirs
    classpath = sourceSets.apiTest.runtimeClasspath
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "passed", "skipped", "failed"
    }
    configure {
        systemProperties = ['spring.profiles.active': System.env.SPRING_PROFILE]
    }
}

spotbugs {
    spotbugsTest.enabled = false
    spotbugsIntegrationTest.enabled = false
    spotbugsApiTest.enabled = false
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}