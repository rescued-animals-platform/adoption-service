buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.0.1'
    }
}

plugins {
    id "org.springframework.boot" version "2.0.2.RELEASE"
    id "info.solidsoft.pitest" version "1.3.0"
}

apply plugin: 'java'
apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/pitest.gradle'
apply from: 'gradle/appengine.gradle'
apply from: 'gradle/sourceset.gradle'
apply from: 'gradle/idea.gradle'

group 'ec.animal.adoption'
version '0.0.1-SNAPSHOT'

bootJar {
    baseName = 'adoption-service'
    version = '0.0.1-SNAPSHOT'
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
    mavenCentral()
}

configurations {
    integrationTestCompile.extendsFrom(testCompile)
    integrationTestRuntime.extendsFrom(testRuntime)
    apiTestCompile.extendsFrom(testCompile)
    apiTestRuntime.extendsFrom(testRuntime)
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task apiTest(type: Test) {
    testClassesDirs = sourceSets.apiTest.output.classesDirs
    classpath = sourceSets.apiTest.runtimeClasspath
}

tasks.withType(Test) {
    clean.execute()
    testLogging {
        exceptionFormat "full"
        events "passed", "skipped", "failed"
    }
    configure {
        systemProperties = ['spring.profiles.active': System.env.SPRING_PROFILE]
    }
}

bootRun {
    configure {
        systemProperties = ['spring.profiles.active': System.env.SPRING_PROFILE]
    }
}