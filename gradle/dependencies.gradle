ext {
    springCloudDependenciesVersion = "2021.0.2"
    mapstructVersion = "1.5.1.Final"
    lombokVersion = "1.18.24"
    cloudinaryVersion = "1.25.0"
    apacheCommonsIoVersion = "2.6"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudDependenciesVersion}"
    }
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    implementation("org.springframework.boot:spring-boot-starter-jetty")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-oauth2-resource-server")
    implementation("org.springframework.security:spring-security-oauth2-jose")
    implementation("org.springframework.security:spring-security-config")
    implementation("org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j")
    implementation("com.google.code.findbugs:jsr305:3.0.2") // Fixes Warning: unknown enum constant When.MAYBE
    implementation("com.cloudinary:cloudinary-http44:${cloudinaryVersion}")
    implementation("commons-io:commons-io:${apacheCommonsIoVersion}")
    implementation('org.postgresql:postgresql:42.3.4')
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation("org.projectlombok:lombok:${lombokVersion}")

    runtimeOnly("org.flywaydb:flyway-core:6.3.2")
    runtimeOnly('net.logstash.logback:logstash-logback-encoder:7.1.1')

    annotationProcessor("org.mapstruct:mapstruct-processor:${mapstructVersion}")
    annotationProcessor("org.projectlombok:lombok-mapstruct-binding:0.2.0")
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
}

testing {
    suites {
        configureEach {
            dependencies {
                implementation("org.springframework.boot:spring-boot-starter-test") {
                    exclude module: "junit"
                    exclude module: "junit-vintage-engine"
                }
            }
        }

        test {
            dependencies {
                implementation('nl.jqno.equalsverifier:equalsverifier:3.10')
                implementation("org.unitils:unitils-core:3.4.6")
            }
        }

        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project()
                implementation("commons-io:commons-io:${apacheCommonsIoVersion}")
            }
        }

        apiTest(JvmTestSuite) {
            dependencies {
                implementation project()
                implementation("org.springframework.boot:spring-boot-starter-webflux")
            }
        }

        archUnitTest(JvmTestSuite) {
            dependencies {
                implementation project()
                implementation('com.tngtech.archunit:archunit-junit5:1.0.1')
            }
        }
    }
}